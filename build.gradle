buildscript {
    ext.kotlin_version = '1.3.21'
    
    ext.spring_boot_version = '2.1.0.RELEASE'
    
    ext.flyway_version = '5.2.1'
    
    ext.jaxb_api_version = '2.3.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //spring
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        // Required for Kotlin integration for spring
        // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        //fixes no-arg constructor errors
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
    }
}

//db migrations
plugins {
    id "org.flywaydb.flyway" version "5.2.1"
}

group 'ru.scp'
version '1.0.9'

apply plugin: 'java'
apply plugin: 'kotlin'
// See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: "kotlin-spring"
//use entities with data classes
apply plugin: "kotlin-jpa"

//spring boot
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//artifact
bootWar {
    project.logger.lifecycle('property ' + project.hasProperty('suffix'))
    dependsOn 'setProductionConfig'
    def suffix = ""
    if (project.hasProperty("suffix")) {
        suffix = project.property("suffix")
    }
    baseName = 'scp-quiz' + suffix
    version = ''
}

task setProductionConfig() {
    project.logger.lifecycle('setProductionConfig')
    group = "other"
    description = "Sets the environment for production mode"
    doFirst {
        if (project.property("suffix") == "-test") {
            /*
           notice the file location ("./build/resources/main/application.properties"),
           it refers to the file processed and already in the build folder, ready
           to be packed, if you use a different folder from `build`, put yours
           here
        */
            ant.propertyfile(file: "./build/resources/main/application.properties") {
                entry(key: "spring.profiles.active", value: "test")
            }
        }

    }
    doLast {
        // maybe put some notifications in console here
    }
}

repositories {
    mavenCentral()
    jcenter()
}

//to be able to run with task args to define correct properties file
//i.e. `bootRun -Dspring.profiles.active=dev`
bootRun.systemProperties = System.properties as Map<String, ?>


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
    //fix classNotFound
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //spring
    compile "org.springframework.boot:spring-boot-starter-web"
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    //info
    compile "org.springframework.boot:spring-boot-starter-actuator"
    //data
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    //security
    compile "org.springframework.boot:spring-boot-starter-security"
    //oauth2
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.3.0.RELEASE'
    //spring END

    //postgresql
    compile 'org.postgresql:postgresql:42.2.5'

    //db migrations
    compile "org.flywaydb:flyway-core:$flyway_version"

    //rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.1"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    //Retrofit
    implementation('com.squareup.retrofit2:retrofit:2.4.0') { exclude module: 'okhttp' }
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'

    //google
    implementation 'com.google.api-client:google-api-client:1.24.1'

    //email
    compile 'org.springframework:spring-context-support:5.1.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-mail:2.1.0.RELEASE'

    //fix tomcat deploy error
    compile 'com.sun.activation:javax.activation:1.2.0'
    compile 'javax.transaction:javax.transaction-api:1.2'
    compile "javax.xml.bind:jaxb-api:$jaxb_api_version"
    compile "org.glassfish.jaxb:jaxb-runtime:$jaxb_api_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

noArg {
    annotation("ru.scp.quiz.utils.NoArgConstructor")
}